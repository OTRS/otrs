
=head1 NAME

Proc::Daemon - Run Perl program(s) as a daemon process.




=head1 SYNOPSIS

    use Proc::Daemon;

    $daemon = Proc::Daemon->new(
        work_dir => '/my/daemon/directory',
        .....
    );

    $Kid_1_PID = $daemon->Init;

    unless ( $Kid_1_PID ) {
        # code executed only by the child ...
    }

    $Kid_2_PID = $daemon->Init( { 
                    work_dir     => '/other/daemon/directory',
                    exec_command => 'perl /home/my_script.pl',
                 } );

    $pid = $daemon->Status( ... );

    $stopped = $daemon->Kill_Daemon( ... );




=head1 DESCRIPTION

This module can be used by a Perl program to initialize itself as a daemon
or to execute (C<exec>) a system command as daemon. You can also check the status
of the daemon (alive or dead) and you can kill the daemon.

A daemon is a process that runs in the background with no controlling
terminal. Generally servers (like FTP, HTTP and SIP servers) run as daemon
processes. Do not make the mistake to think that a daemon is a server. ;-)

Proc::Daemon does the following:


=over 4

=item 1

The script forks a child.


=item 2

The child changes the current working directory to
the value of 'work_dir'.


=item 3

The child clears the file creation mask.


=item 4

The child becomes a session leader, which detaches the program from the
controlling terminal.


=item 5

The child forks another child (the final daemon process). This prevents
the potential of acquiring a controlling terminal at all and detaches the
daemon completely from the first parent.


=item 6

The second child closes all open file descriptors (unless you define
C<dont_close_fh> and/or C<dont_close_fd>).


=item 7

The second child opens STDIN, STDOUT and STDERR to the location defined in the
constructor (C<new>).


=item 8

The second child returns to the calling script, or the program defined
in 'exec_command' is executed and the second child never returns.


=item 9

The first child transfers the PID of the second child (daemon) to the
parent. Additionally the PID of the daemon process can be written into a file
if 'pid_file' is defined. Then the first child exits.


=item 10

If the parent script is looking for a return value, then the PID(s) of the
child/ren will be returned. Otherwise the parent will exit.

=back


NOTE: Because of the second fork the daemon will not be a session-leader and
therefore Signals will not be send to other members of his process group. If
you need the functionality of a session-leader you may want to call
POSIX::setsid() manually at your daemon.


INFO: Since C<fork> is not performed the same way on Windows systems as on
Linux, this module does not work with Windows. Patches appreciated!




=head1 CONSTRUCTOR


=over 4

=item new ( %ARGS )

The constructor creates a new Proc::Daemon object based on the hash %ARGS. The
following keys from %ARGS are used:


=over 8

=item work_dir

Defines the path to the working directory of your daemon. Defaults to C</>.


=item setuid

Sets the real user identifier (C<< $< >>) and the effective user identifier
(C<< $> >>) for the daemon process using C<POSIX::setuid( ... )>, in case you
want to run your daemon under an other user than the parent. Obviously the
first user must have the rights to switch to the new user otherwise it will
stay the same. It is helpful to define the argument C<setuid> if you start your
script at boot time by init with the superuser, but wants the daemon to run
under a normal user account.

=item setgid

Sets the real group identifier (C<$(>) and the effective group identifier
(C<$)>) for the daemon process using C<POSXI::setgid( ... )>, just like
C<setuid>.  As with C<setuid>, the first user must have the rights to switch to the
new group, otherwise the group id will not be changed.


=item child_STDIN

Defines the path to STDIN for your daemon. Defaults to C</dev/null>. Default
Mode is '<' (read). You can define other Mode the same way as you do using
Perls C<open> in a two-argument form.


=item child_STDOUT

Defines the path where the output of your daemon will go. Defaults to
C</dev/null>. Default Mode is '+>' (write/read). You can define other Mode the
same way as you do using Perls C<open> in a two-argument form.


=item child_STDERR

Defines the path where the error output of your daemon will go. Defaults to
C</dev/null>. Default Mode is '+>' (write/read). You can define other Mode the
same way as you do using Perls C<open> in a two-argument form, see example
below.


=item dont_close_fh

If you define it, it must be an arrayref with file handles you want to preserve
from the parent into the child (daemon). This may be the case if you have code
below a C<__DATA__> token in your script or module called by C<use> or
C<require>.

    dont_close_fh => [ 'main::DATA', 'PackageName::DATA', $my_filehandle, ... ],

You can add any kind of file handle to the array (expression in single quotes or
a scalar variable), including 'STDIN', 'STDOUT' and 'STDERR'. Logically the path
settings from above (C<child_STDIN>, ...) will be ignored in this case.

DISCLAIMER: Using this argument may not detach your daemon fully from the
parent! Use it at your own risk.


=item dont_close_fd

Same function and disclaimer as C<dont_close_fh>, but instead of file handles
you write the numeric file descriptors inside the arrayref.


=item pid_file

Defines the path to a file (owned by the parent user) where the PID of the
daemon process will be stored. Defaults to C<undef> (= write no file).


=item file_umask

Defines umask for C<pid_file>, C<child_STDIN>, C<child_STDOUT> and
C<child_STDERR> files. Defaults to C<066> (other users may not modify or
read the files).


=item exec_command

Scalar or arrayref with system command(s) that will be executed by the
daemon via Perls C<exec PROGRAM_LIST>. In this case the child will never
return to the parents process!

=back


Example:

    my $daemon = Proc::Daemon->new(
        work_dir     => '/working/daemon/directory',
        child_STDOUT => '/path/to/daemon/output.file',
        child_STDERR => '+>>debug.txt',
        pid_file     => 'pid.txt',
        exec_command => 'perl /home/my_script.pl',
      # or:
      # exec_command => [ 'perl /home/my_script.pl', 'perl /home/my_other_script.pl' ],
    );

In this example:

=over 8

=item *

the PID of the daemon will be returned to C<$daemon> in the parent process
and a pid-file will be created at C</working/daemon/directory/pid.txt>.

=item *

STDOUT will be open with Mode '+>' (write/read)
to C</path/to/daemon/output.file> and STDERR will be open
to C</working/daemon/directory/debug.txt> with Mode '+>>' (write/read opened for
appending).

=item *

the script C</home/my_script.pl> will be executed by C<perl> and run as
daemon. Therefore the child process will never return to this parent script.

=back

=back




=head1 METHODS

=over 4

=item Init( [ { %ARGS } ] )

Become a daemon.

If used for the first time after C<new>, you call C<Init> with the object
reference to start the daemon.

    $pid = $daemon->Init();

If you want to use the object reference created by C<new> for other daemons,
you write C<Init( { %ARGS } )>. %ARGS are the same as described in
C<new>. Notice that you shouldn't call C<Init()> without argument in this case,
or the next daemon will execute and/or write in the same files as the first
daemon. To prevent this use at least an empty anonymous hash here.

    $pid = $daemon->Init( {} );
    @pid = $daemon->Init( {
        work_dir     => '/other/daemon/directory',
        exec_command => [ 'perl /home/my_second_script.pl', 'perl /home/my_third_script.pl' ],
    } );

If you don't need the Proc::Daemon object reference in your script, you
can also use the method without object reference:

    $pid = Proc::Daemon::Init();
    # or
    $pid = Proc::Daemon::Init( { %ARGS } );

C<Init> returns the PID (scalar) of the daemon to the parent, or the PIDs
(array) of the daemons created if C<exec_command> has more then one program
to execute. See examples above.

C<Init> returns 0 to the child (daemon).

If you call the C<Init> method in the context without looking for a return value
(void context) the parent process will C<exit> here like in earlier versions:

    Proc::Daemon::Init();


=item Status( [ $ARG ] )

This function checks the status of the process (daemon). Returns the PID number
(alive) or 0 (dead).

$ARG can be a string with:

=over 8

=item *

C<undef>, in this case it tries to get the PID to check out of the object
reference settings.

=item *

a PID number to check.

=item *

the path to a file containing the PID to check.

=item *

the command line entry of the running program to check. This requires
L<Proc::ProcessTable> to be installed.

=back


=item Kill_Daemon( [ $ARG [, SIGNAL] ] )

This function kills the Daemon process. Returns the number of processes
successfully killed (which mostly is not the same as the PID number), or 0 if
the process wasn't found. 

$ARG is the same as of C<Status()>. SIGNAL is an optional signal name or number
as required by Perls C<kill> function and listed out by C<kill -l> on your
system. Default value is 9 ('KILL' = non-catchable, non-ignorable kill).


=item Fork

Is like the Perl built-in C<fork>, but it retries to fork over 30 seconds if
necessary and if possible to fork at all. It returns the child PID to the
parent process and 0 to the child process. If the fork is unsuccessful
it C<warn>s and returns C<undef>.

=back




=head1 OTHER METHODS

Proc::Daemon also defines some other functions. See source code for more
details:

=over 4

=item OpenMax( [ $NUMBER ] )

Returns the maximum file descriptor number. If undetermined $NUMBER will be
returned.


=item adjust_settings

Does some fixes/adjustments on the C<new> settings together with C<fix_filename>.


=item fix_filename( $KEYNAME )

Prevents double use of same filename in different processes.


=item get_pid( [ $STRING ] )

Returns the wanted PID if it can be found.


=item get_pid_by_proc_table_attr( $ATTR, $MATCH )

Returns the wanted PID by looking into the process table, or C<undef>. Requires
the C<Proc::ProcessTable> module to be installed.


=back




=head1 NOTES

C<Proc::Daemon::init> is still available for backwards capability.

Proc::Daemon is now taint safe (assuming it is not passed any tainted parameters).


=head1 AUTHORS

Primary-maintainer and code writer until version 0.03:

=over 4

=item *

Earl Hood, earl@earlhood.com, http://www.earlhood.com/

=back


Co-maintainer and code writer since version 0.04 until version 0.14:

=over 4

=item *

Detlef Pilzecker, http://search.cpan.org/~deti/,
http://www.secure-sip-server.net/

=back


Co-maintainer and code writer since version 0.15:

=over 4

=item *

Pavel Denisov, http://search.cpan.org/~akreal/

=back




=head1 CREDITS

Initial implementation of C<Proc::Daemon> derived from the following sources:

=over 4

=item *

"Advanced Programming in the UNIX Environment" by W. Richard Stevens.
Addison-Wesley, Copyright 1992.

=item *

"UNIX Network Programming", Vol 1, by W. Richard Stevens.
Prentice-Hall PTR, Copyright 1998.

=back




=head1 PREREQUISITES

This module requires the C<POSIX> module to be installed.

The C<Proc::ProcessTable> module is not essentially required but it can be
useful if it is installed (see above).



=head1 REPOSITORY

L<https://github.com/akreal/Proc-Daemon>




=head1 SEE ALSO

L<perl(1)>, L<POSIX>, L<Proc::ProcessTable>




=head1 COPYRIGHT

This module is Copyright (C) 1997-2015 by Earl Hood, Detlef Pilzecker and Pavel Denisov.

All Rights Reserved.

This module is free software. It may be used, redistributed and/or modified
under the same terms as Perl itself.

